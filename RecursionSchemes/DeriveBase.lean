import Lean
open Lean Elab Command Term Tactic
open Lean.Parser.Term
open Lean.Parser.Command
open Lean.Elab.Deriving

-- [todo] this must already exist.
def Name.mapHead (f : String →String) : Name →Name
  | Name.str p s _ => Name.mkStr p (f s)
  | n => n

def NameMap.modifyCol [EmptyCollection α] (visit: α → α) (n : NameMap α) (k : Name) : NameMap α :=
  n.find? k |>.getD ∅ |> visit |> n.insert k

def mkPrisms (decl : Name) : CommandElabM Unit := do
  if not (← isInductive decl) then
    throwError "{decl} must be an inductive datatype."
  let indVal ← getConstInfoInduct decl
  if isStructure (← getEnv) indVal.name then
    throwError "{decl} structures have projectors already"
  if indVal.numIndices > 0 then
    -- It should be possible to auto-derive some optics using the method as below
    -- But the result is usually confusing so it's better to not support it and
    -- get the users to make bespoke optics.
    throwError "getters and setters derivation not supported for indexed inductive datatype {decl}."
  for ctor in indVal.ctors do
    let ctorInfo ← Lean.getConstInfoCtor ctor
    let cmds ← liftTermElabM none <| Lean.Meta.forallTelescopeReducing ctorInfo.type fun xs type => do
      let mut cmds := #[]
      -- [todo] I think you have to do some macro hygeine here with eraseMacroScopes and mkFreshUserName but idk
      let xsdecls ← liftM <| xs.mapM Lean.Meta.getFVarLocalDecl
      let params := xsdecls[:ctorInfo.numParams].toArray
      let fields := xsdecls[ctorInfo.numParams:].toArray
      let fieldPatterns ← fields.mapM (fun f => mkIdent <$> mkFreshUserName f.userName)
      let implicitBinders ← params |>.mapM (fun x => `(implicitBinderF| { $(mkIdent x.userName) }))
      let ctorPattern ← `($(mkIdent ctorInfo.name):ident $fieldPatterns:term*)
      for fieldIdx in List.range ctorInfo.numFields do
        let field := fields[fieldIdx]
        let fieldPat := fieldPatterns[fieldIdx]
        let outType ← PrettyPrinter.delab type
        let fieldType ← PrettyPrinter.delab field.type
        -- [todo] check that field has friendly userName. If it doesn't then don't derive the optics.
        -- [todo] if there are no clashes, then you can drop the constructor name.
        -- [todo] if the same field name appears on multiple ctors, we can make a multi-ctor version of the optics where we drop the ctor name prefix.
        --        additionally, if the field name appears on all constructors we can produce a Lens version and drop the `?`.
        -- [todo] How to antiquote in docstrings?

        -- ①: T.𝑐.𝑎? : T → Option α
        let defname : Name := ctorInfo.name ++ Name.mapHead (· ++ "?") field.userName
        cmds := cmds.push <|← `(
          /-- If the given `$(mkIdent indVal.name)` is a `$(mkIdent ctorInfo.name)`,
          returns the value of the `$(mkIdent field.userName)` field, otherwise returns `none`. -/
          def $(mkIdent defname):ident $implicitBinders:explicitBinder*
          : $outType → Option $fieldType
          | $ctorPattern => some $fieldPat
          | x => none
        )

        -- ②: T.𝑐.𝑎! : T → α
        let defname : Name := ctorInfo.name ++ Name.mapHead (· ++ "!") field.userName
        cmds := cmds.push <|← `(
          /-- If the given `$(mkIdent indVal.name)` is a `$(mkIdent ctorInfo.name)`,
          returns the value of the `$(mkIdent field.userName)` field, otherwise panics. -/
          def $(mkIdent defname):ident $implicitBinders:explicitBinder* [Inhabited $fieldType]
          : $outType → $fieldType
          | $ctorPattern => $fieldPat
          | x =>
            let n := $(quote ctor)
            panic! s!"expected constructor {n}")

        -- ③: T.𝑐.with𝑎 : α → T → T
        let defname : Name := ctorInfo.name ++ Name.mapHead (fun n => s!"with{n.capitalize}") field.userName
        let a ← mkIdent <$> mkFreshUserName `a
        cmds := cmds.push <|← `(
          /-- If the given `$(mkIdent indVal.name)` is a `$(mkIdent ctorInfo.name)`,
          replaces the value of the `$(mkIdent field.userName)` field with the given value.
          Otherwise acts as the identity function. -/
          def $(mkIdent defname):ident $implicitBinders:explicitBinder*
          : $fieldType → $outType → $outType
          | $a, $ctorPattern => $(mkIdent ctorInfo.name):ident $(fieldPatterns.modify fieldIdx (fun _ => a)):term*
          | _, x => x
        )

        -- ④: T.𝑐.modify𝑎 : (α → α) → T → T
        let defname : Name := ctorInfo.name ++ Name.mapHead (fun n => s!"modify{n.capitalize}") field.userName
        let a ← mkIdent <$> mkFreshUserName `a
        let outPat ← fieldPatterns.modifyM fieldIdx (fun q => `( ($a <| $q) ))
        cmds := cmds.push <|← `(
          /-- If the given `$(mkIdent indVal.name)` is a `$(mkIdent ctorInfo.name)`,
          modifies the value of the `$(mkIdent field.userName)` field with the given `visit` function.  -/
          def $(mkIdent defname):ident $implicitBinders:explicitBinder*
          : (visit : $fieldType → $fieldType) → $outType → $outType
          | $a, $ctorPattern => $(mkIdent ctorInfo.name):ident $outPat:term*
          | _, x => x
        )

        -- ⑤: T.𝑐.modifyM𝑎 : (α → M α) → T → M T
        let defname : Name := ctorInfo.name ++ Name.mapHead (fun n => s!"modifyM{n.capitalize}") field.userName
        let visit ← mkIdent <$> mkFreshUserName `visit
        let x ← mkIdent <$> mkFreshUserName `x
        let outPat := fieldPatterns.modify fieldIdx (fun _ => x)
        cmds := cmds.push <|← `(
          /-- Runs the given `visit` function on the `$(mkIdent field.userName)` argument of `$(mkIdent ctorInfo.name)`.
          Performing the pure op if the given `$(mkIdent indVal.name)` is not a `$(mkIdent ctorInfo.name)`.

          This is also known as the affine traversal of the field in the van Laarhoven representation.
           -/
          def $(mkIdent defname):ident $implicitBinders:explicitBinder*
            {M} [Pure M] [Functor M]
            : (visit : $fieldType → M $fieldType) → $outType → M $outType
            | $visit, $ctorPattern => (fun $x => $(mkIdent ctorInfo.name):ident $outPat:term*) <$> $visit $fieldPat
            | _, x => pure x
        )

      return cmds
    cmds.forM elabCommand

elab "derive_optics" decl:ident : command =>
  mkPrisms decl.getId

inductive Wow
  | foo (cheese : Nat) (plops : Wow)
  | bar (lemon : String) (posset : Nat)
derive_optics Wow


inductive MyVec (α β : Type) :  Type
  | nil : MyVec α β
  | cons (head : α × β) (tail : MyVec α β) : MyVec α β

#check @MyVec.cons

derive_optics MyVec

def t := (Wow.bar "asdf" 3)

#check Wow.bar.lemon?
#check Wow.bar.withLemon
#eval Wow.bar.lemon? t
#eval Wow.foo.cheese? t
#eval Wow.bar.lemon? $  Wow.bar.withLemon "qwerty" t
#eval Wow.bar.lemon? $  Wow.bar.modifyLemon (· ++ "qwerty") t
#eval Wow.bar.lemon? <$>  @Wow.bar.modifyMLemon (OptionT Id) _ _ (fun x => pure (x ++ "qwerty")) t

#eval List.iota 3
#eval List.range 3